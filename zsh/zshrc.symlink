# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export DOTFILES=$HOME/.dotfiles

# export NGINX_PATH=/opt/nginx
# export PGDATA=/usr/local/var/postgres

# Do folder
export PROJECTS=~/Do

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# source path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# source every .zsh file but path and completion in the repo.
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files